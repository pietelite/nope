import org.spongepowered.gradle.plugin.config.PluginLoaders
import org.spongepowered.plugin.metadata.model.PluginDependency

plugins {
    id 'java-library'
    id 'org.spongepowered.gradle.plugin' version '2.0.0'
}

archivesBaseName = 'nope'
group = 'com.minecraftonline'
version = '0.8.0'
description = "A protections plugin"

repositories {
    mavenCentral()
}

sponge {
    apiVersion("8.0.0-SNAPSHOT")
    license("LICENSE")
    loader {
        name(PluginLoaders.JAVA_PLAIN)
        version("1.0")
    }
    plugin("nope") {
        displayName("Nope")
        entrypoint("com.minecraftonline.nope.sponge.SpongeNope")
        description("A protections plugin")
        links {
            homepage("https://spongepowered.org")
            source("https://spongepowered.org/source")
            issues("https://spongepowered.org/issues")
        }
        contributor("PietElite") {
            description("Author")
        }
        dependency("spongeapi") {
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(false)
        }
    }
}

java {
    sourceCompatibility = JavaVersion.toVersion(8)
    targetCompatibility = JavaVersion.toVersion(8)
}

tasks.withType(JavaCompile.class).configureEach {
    options.encoding = "utf-8"
}

// Make sure all tasks which produce archives (jar, sources jar, javadoc jar, etc) produce more consistent output
tasks.withType(AbstractArchiveTask.class).configureEach {
    reproducibleFileOrder = true
    preserveFileTimestamps = false
}
//////////////////////////

test {
    useJUnitPlatform()
}

dependencies {
    // Sponge
    implementation "org.spongepowered:spongeapi:8.0.0-SNAPSHOT"
    annotationProcessor "org.spongepowered:spongeapi:8.0.0-SNAPSHOT"

    // JUnit
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'

    // Lombok
    implementation 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'

    testImplementation 'org.projectlombok:lombok:1.18.16'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.16'

    // IntelliJ Annotations
    implementation 'org.jetbrains:annotations:16.0.2'

}
